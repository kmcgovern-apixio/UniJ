/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2018 Tomasz Linkowski.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  // https://github.com/ben-manes/gradle-versions-plugin
  id "com.github.ben-manes.versions" version "0.21.0"

  // https://aalmiray.github.io/kordamp-gradle-plugins/
  id "org.kordamp.gradle.base" version "0.14.0"

  // https://github.com/melix/jmh-gradle-plugin
  id "me.champeau.gradle.jmh" version "0.4.8" apply false
}

wrapper {
  gradleVersion = "5.2.1"
  distributionType = Wrapper.DistributionType.ALL
}

config {
  info {
    name = "UniJ"
    vendor = "Tomasz Linkowski"
    inceptionYear = "2018"
    description = "Universal cross-JDK API provider for immutable collections"

    links {
      website = "https://unij.tlinkowski.pl/"
      issueTracker = "https://github.com/tlinkowski/UniJ/issues"
      scm = "https://github.com/tlinkowski/UniJ.git"
    }

    people {
      person {
        id = "tlinkowski"
        name = "Tomasz Linkowski"
        url = "https://tlinkowski.pl/"
        roles = ["developer"]
      }
    }
  }

  license {
    licenses {
      license {
        id = "Apache-2.0"
      }
    }
  }
}

ext.lombokVersion = "1.18.6" // https://projectlombok.org/changelog
ext.slf4jVersion = "1.7.26" // https://www.slf4j.org/

allprojects {
  repositories {
    mavenCentral()
  }
}

subprojects {
  group = "pl.tlinkowski.unij"

  apply plugin: "java" // https://docs.gradle.org/current/userguide/java_plugin.html
  apply plugin: "groovy" // https://docs.gradle.org/current/userguide/groovy_plugin.html
  apply plugin: "jacoco" // https://docs.gradle.org/current/userguide/jacoco_plugin.html
  apply plugin: "me.champeau.gradle.jmh"

  apply from: "$rootDir/gradle/configure-compile-tasks.gradle"

  configurations {
    testCompileOnly.extendsFrom compileOnly
  }

  dependencies {
    compileOnly group: "org.projectlombok", name: "lombok", version: lombokVersion
    annotationProcessor group: "org.projectlombok", name: "lombok", version: lombokVersion

    compileOnly group: "com.google.code.findbugs", name: "jsr305", version: "3.0.2"
    compileOnly group: "org.jetbrains.kotlin", name: "kotlin-annotations-jvm", version: "1.3.21"
  }

  /**
   * Code Coverage (JaCoCo)
   * https://github.com/jacoco/jacoco
   */
  jacocoTestReport {
    reports {
      xml.enabled = true
      html.enabled = project.hasProperty("generateHtmlJacocoReport")
    }
  }
  check.dependsOn jacocoTestReport

  jacocoTestCoverageVerification {
    violationRules {
      rule {
        limit {
          counter = 'LINE'
          minimum = 0.99
        }
      }
    }
  }
  check.dependsOn jacocoTestCoverageVerification

  /**
   * BENCHMARKS
   * http://openjdk.java.net/projects/code-tools/jmh/
   */
  jmh {
    /*
     * Note that when using "separateClasspathJAR" option and running the benchmark on Windows,
     * the following four resources must be on the same drive (otherwise, "'other' has different root" is thrown):
     * 1) this project
     * 2) JDK
     * 3) Gradle user home dir (can be set using GRADLE_USER_HOME environment variable)
     * 4) TEMP dir (can be set using System properties through "java.io.tmpdir")
     */
    jvmArgsAppend = ["-Djmh.separateClasspathJAR=true"] // https://bugs.openjdk.java.net/browse/CODETOOLS-7902106
  }
}

apply from: "$rootDir/gradle/dependency-updates.gradle"
apply from: "$rootDir/gradle/ide.gradle"
